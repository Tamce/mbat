
set player.hp = 100
set player.atk = 15
set player.def = 10
set player.score = 0

set status = 0

echol ===============================================
echol  __  __ ______        _____  ____  _     ____
echol |  \/  | __ ) \      / / _ \|  _ \| |   |  _ \
echol | |\/| |  _ \\ \ /\ / / | | | |_) | |   | | | |
echol | |  | | |_) |\ V  V /| |_| |  _ <| |___| |_| |
echol |_|  |_|____/  \_/\_/  \___/|_| \_\_____|____/
echol
echol   -------------------------------------------
echol          该系统基于微型解释器 MBAT
echol         该系统及 MBAT 由 Tamce 设计
echol ===============================================
echo 请问您怎么称呼 > 
input player.name
echol 欢迎您来到 MBWORLD: ${player.name}！
echol 让我们来一场刺激的文字冒险游戏吧！
echol 这是你的初始状态：
set printStatus() = #1
goto printStatus()
:tag #1
echol ################ 注意 #########################
echol 当程序提示 ... 的时候表示等待用户按下回车
echol 当程序提示  >  的时候表示等待用户输入并按下回车
echol ###############################################
echol 准备好了就按下回车开始吧！
echo ...
input choice

:tag scene1
echol
echol 你推开了 MBWORD 的大门，走进了迷宫
echol 来到大厅，你突然发现你面前有一个样子奇特的神秘生物
:tag scene1-choice
echol ------------------------------------------------------
echol                     要怎么做？
echol ------------------------------------------------------
echol  a. 尝试向其表示友好   b. 不理它   c. 直接向其发起攻击
echol ------------------------------------------------------
:tag scene1-input
echo > 
input choice
if [${choice} = a] goto scene2
if [${choice} = b] goto scene3
if [${choice} = c] set battle.first = 1
if [${choice} = c] goto scene4
echol 请选择(a,b,c)其中一个喔~
goto scene1-input

:tag scene2
# 向奇特生物表示友好
echol 你尝试对神秘生物表示友好，在你发现语言不通时，你选择用肢体语言表达意思
echo ...
input choice
echol 神秘生物咕哝了几声，然后突然向你扑来！
echol 你沉浸在自己的肢体语言中被突如起来的攻击吓到！
echo ...
set battle.first = 0
goto scene4

:tag scene3
# 不理奇特生物
echol
echol 你选择不理睬神秘生物
echol 当你准备偷偷的绕过神秘生物时，突然，神秘生物发现了你！
echo ...
input choice
echol ------------------------------------------------------
echol                     要怎么做？
echol ------------------------------------------------------
echol       a. 先发制人，对其发起攻击   b. 向迷宫深处跑
echol            c. 我是和平主义者，尝试与其交流
echol ------------------------------------------------------
:tag scene3-input
echo > 
input choice
if [${choice} = a] set battle.first = 1
if [${choice} = a] goto scene4
if [${choice} = b] goto scene5
if [${choice} = c] goto scene2
echol 请选择(a,b,c)其中一个喔~
goto scene3-input

:tag scene4
# 对奇特生物发起攻击
set battle.name = 神秘生物
set battle.hp = 15
set battle.atk = 14
set battle.def = 10
set battle.bonous.score = 5
set battle.bonous.atk = 2
set battle.bonous.def = 1
set battle() = #2
goto battle()
:tag #2
# 打败一次增加一次计数
calc status = ${status} + 1
goto scene6

:tag scene5
# 选择逃过了神秘生物
# 选择逃跑计数归零
set status = 0
goto scene6

:tag scene6
# 打败了奇特生物，向前继续进发
echol
echol 你继续向内部深入前进
echol 在你走到前面时，突然发现前面出现了岔路
echo ...
input choice
echol 在岔路口可以向一左一右两个完全相反的方向继续走
echol ------------------------------------------------------
echol                       要怎么做？
echol ------------------------------------------------------
echol              a. 向左走         b. 向右走
echol                     c. 原路返回
echol ------------------------------------------------------
:tag scene6-input
echo > 
input choice
if [${choice} = a] goto left
if [${choice} = b] goto right
if [${choice} = c] goto back
echol 请选择(a,b,c)其中一个喔~
goto scene6-input

:tag back
echol 你选择了原路返回，于是你调过身子开始往回走
if [${status} = 0] goto back-0
goto repeat
:tag back-0
echol 突然！你发现之前被你躲过的神秘生物正在朝你飞奔而来
echol 你发现神秘生物似乎正做出攻击状
echol ------------------------------------------------------
echol                       要怎么做？
echol ------------------------------------------------------
echol              a. 逃走         b. 主动发起攻击
echol                    c. "什...什么？"
echol ------------------------------------------------------
:tag back-input
echo > 
input choice
if [${choice} = a] goto back-a

if [${choice} = b] set battle.first = 1
if [${choice} = b] goto scene4

if [${choice} = c] echol 你一时有点没反应过来，神秘生物突然发起了攻击！
if [${choice} = c] echo ...
if [${choice} = c] input choice
if [${choice} = c] set battle.first = 0
if [${choice} = c] goto scene4
echol 请选择(a,b,c)其中一个喔~
goto back-input

:tag back-a
echol 你选择逃走，但是一时慌乱，头撞到了墙上
echo ...
input choice
echol 你感到头晕眼花，摸了摸头上，似乎出了点血
calc player.hp = ${player.hp} - 5
echol ${player.name} 损失了 5 点 HP，当前 HP 为 ${player.hp}
echo ...
input choice
echol 神秘生物突然眼冒红光，似乎变得暴躁了起来！
echol 神秘生物的生命、攻击力和防御力似乎都增加了！
echo ...
input choice
echol 神秘生物突然快速朝你扑来..
set battle.name = 发狂的神秘生物
set battle.hp = 25
set battle.atk = 17
set battle.def = 13
set battle.bonous.score = 8
set battle.bonous.atk = 1
set battle.bonous.def = 2
set battle.first = 0
set battle() = #5
goto battle()
:tag #5
echol 好不容易打败了发狂的神秘生物，你拖着疲惫的身子向前进
calc status = ${status} + 1
echo ...
input choice
goto repeat



:tag right
echol 你选择在岔路口向右走
echo ...
input choice
echol 你仿佛看到了迷宫的出口就在前方
echol 你兴奋的向出口跑去
echo ...
input choice
echol 突然，一个巨型生物挡住了你的去路
echol ------------------------------------------------------
echol                       要怎么做？
echol ------------------------------------------------------
echol              a. 没有退路了！      b. 主动发起攻击
echol                    c. "发..发生什么了？"
echol ------------------------------------------------------
:tag right-input
echo > 
input choice
if [${choice} = a] set battle.first = 1
if [${choice} = a] goto final
if [${choice} = b] set battle.first = 1
if [${choice} = b] goto final
if [${choice} = c] set battle.first = 0
if [${choice} = c] goto final
echol 请选择(a,b,c)其中一个喔~
goto right-input
:tag final
set battle.name = 巨型生物
set battle.atk = 27
set battle.def = 18
set battle.hp = 30
set battle.bonous.score = 10
set battle.bonous.atk = 5
set battle.bonous.def = 2
set battle() = #6
goto battle()
:tag #6
echol 打败了巨型生物，出口就在眼前
echo ...
input choice
echol 你兴奋的推开了大门
echo ...
input choice
echol 发现，门的那边...
echo ...
input choice
echol 就是你最初进入的迷宫........
echo ...
input choice
goto maker


:tag left
echol 你选择在岔路口向左走
:tag repeat
echol 走着走着，你突然意识到地形有点熟悉
echo ...
input choice
echol 突然，你发现你来到了你一开始进入迷宫时的大厅
echo ...
input choice
if [${status} = 0] echol 在大厅里，那个神秘生物依然安静的呆在那里
if [${status} = 0] echol "怎么又回来了..."
if [${status} = 0] goto scene1-choice
if [${status} < 4] echol 你突然发现在大厅之前被你打败的神秘生物又出现了
if [${status} < 4] echol "怎么...回事？？"
if [${status} < 4] goto scene1-choice
# 隐藏剧情
echol 你发现那个神秘生物消失不见
echol 在它原本呆着的地方漂浮着一片发出金色光芒的碎片一样的东西，上面似乎还隐约写着字
echol 碎片发出的光线使你感到十分舒服
echo ...
input choice
echol 你被它深深的吸引了，于是你开始慢慢的朝它移动
echo ...
input choice
echol (脚步声...)
echo ...
input choice
echol (脚步声.....)
echo ...
input choice
echol 走到碎片面前，突然碎片化作了一片羽毛的样子
echol 你轻轻的拿起羽毛
echo ...
input choice
echol 突然你感到一阵眩晕
echo ...
input choice
echol 周围的景色都变了，你似乎被传送到了某个地方
echo ...
input choice
echol 你环顾周围，发现居然是
echo ...
input choice
echol 华南...
echo ...
input choice
echol 华南理工大学！
echo ...
input choice
goto maker

# ======================= Functions ====================

# Function: battle
# Required: battle.name battle.hp battle.atk battle.def battle.first battle.score
:tag battle()
# 先打印战斗信息
set battleInfo() = #3
goto battleInfo()
:tag #3
echo ...
input choice
# 战斗过程
if [${battle.first} > 0] goto battle-player-atk
goto battle-enemy-atk
# 玩家攻击回合
:tag battle-player-atk
calc battle.damage = ${player.atk} - ${battle.def}
calc battle.hp = ${battle.hp} - ${battle.damage}
echo ${player.name} 对 ${battle.name} 发起攻击！造成了 ${battle.damage} 点伤害！
echol ${battle.name} 剩余 HP 变为： ${battle.hp}！
echo ...
input choice
if [${battle.hp} < 0.5] goto battle-enemy-down
goto battle-enemy-atk
# 对方攻击回合
:tag battle-enemy-atk
calc battle.damage = ${battle.atk} - ${player.def}
calc player.hp = ${player.hp} - ${battle.damage}
echo ${battle.name} 对 ${player.name} 进行攻击！造成了 ${battle.damage} 点伤害！
echol ${player.name} 剩余 HP 变为： ${player.hp}！
echo ...
input choice
if [${player.hp} < 0.5] goto player-down
goto battle-player-atk
# 对方倒下了
:tag battle-enemy-down
echol ${battle.name} 倒下了！${player.name} 获得了胜利！
calc player.score = ${player.score} + ${battle.bonous.score}
echo ...
input choice
echol 战斗结束，${player.name} 获得了 ${battle.bonous.score} 分！现在总分为：${player.score}！
if [${battle.bonous.atk} > 0] echol 由于战斗带来的经验，${player.name} 的攻击力增加了！
if [${battle.bonous.def} > 0] echol 由于战斗带来的经验，${player.name} 的防御力增加了！
if [${battle.bonous.atk} > 0] calc player.atk = ${player.atk} + ${battle.bonous.atk}
if [${battle.bonous.atk} > 0] echol ${player.name} 获得了 ${battle.bonous.atk} 点攻击力，现在攻击力为 ${player.atk}！
if [${battle.bonous.def} > 0] calc player.def = ${player.def} + ${battle.bonous.def}
if [${battle.bonous.def} > 0] echol ${player.name} 获得了 ${battle.bonous.def} 点防御力，现在防御力为 ${player.def}！
echo ...
input choice
echol 现在你的状态如下
set printStatus() = #4
goto printStatus()
:tag #4
echo ...
input choice
goto ${battle()}

# Function: battleInfo
# Required: battle.name battle.hp battle.atk, battle.def
# 打印战斗双方信息
:tag battleInfo()
echol
echol =================== 战斗发生 ===================
if [${battle.first} > 0] echol                       先攻
if [${battle.first} < 0.5] echol                        后攻
echol ===============================================
echol 对手 ${battle.name}
echol  HP: ${battle.hp}
echol ATK: ${battle.atk}	DEF: ${battle.def}
echol -----------------------------------------------
echol 玩家 ${player.name}
echol  HP: ${player.hp}
echol ATK: ${player.atk}	DEF: ${player.def}
echol ===============================================
goto ${battleInfo()}

# Function: printStatus
# 打印人物状态
:tag printStatus()
    echol ---------- ${player.name} ----------
    # ============== print HP
    echol    HP: ${player.hp}
    echol   ATK: ${player.atk}    DEF: ${player.def}
    echol Score: ${player.score}
    echol ---------- ${player.name} ----------
goto ${printStatus()}

# ==================== Functions End======================
:tag player-down
echol
echol ${player.name} HP 低于 0！真是遗憾！
# 退出前暂停并提示
:tag gameover
echol ==============================================
echol 游戏结束，欢迎下次游玩~
echol 您最后的结果为：
set printStatus() = #end
goto printStatus()
:tag #end
echo 按下回车退出程序...
input choice
goto eof

:tag maker
echol
echol
echol
echol           MBWORLD 序章
echol ==============================
echol        _____ _   _ ____
echol       | ____| \ | |  _ \
echol       |  _| |  \| | | | |
echol       | |___| |\  | |_| |
echol       |_____|_| \_|____/
echol
echol ==============================
echol          Powered By
echol ==============================
echol     __  __ ____        _
echol    |  \/  | __ )  __ _| |_
echol    | |\/| |  _ \ / _` | __|
echol    | |  | | |_) | (_| | |_
echol    |_|  |_|____/ \__,_|\__|
echol
echol ==============================
echol        Story & Script By
echol ==============================
echol  _____
echol |_   _|_ _ _ __ ___   ___ ___
echol   | |/ _` | '_ ` _ \ / __/ _ \
echol   | | (_| | | | | | | (_|  __/
echol   |_|\__,_|_| |_| |_|\___\___|
echol
echol ==============================
echo ...
input choice
goto gameover